#!/bin/csh -f
########################################################################
#
#  Name: g5_modules
#
#  Purposes -
#    * provide single location for module values
#    * initialize the following:
#      - load library modules
#    * echo expected library module values when queried
#
#  Notes:
#  1. The script will not function correctly if the scriptname
#     definition within the script is incorrect.
#  2. Type "g5_modules help" for usage information.
#
#  Perl Notes:
#  ==========
#  1. You cannot "source" this file from a perl script.
#  2. Instead use the following in your perl script:
#     #--------------------------------
#        use Env;
#        do "g5_modules_perl_wrapper";
#     #--------------------------------
#  3. Use the g5_modules_perl_wrapper file located in the build's bin
#     directory, where a copy of g5_modules also resides. This is important
#     since g5_modules_perl_wrapper uses g5_modules and expects it to be in
#     the same directory.
#
#  Bash Notes:
#  ==========
#  If you are in a bash shell, then source the g5_modules.sh script.
#  > source g5_modules.sh
#
#  REVISION HISTORY
#  31Aug2007  Stassi   Initial version of code
#  15Oct2007  Stassi   Expand list of machines and include defaults
#  15Dec2007  Stassi   Added LD_LIBRARY_PATH functionality
#  21Jul2008  Takacs   New modules and BASEDIR on discover after OS upgrade
#  13Apr2009  Stassi   Updated for pleiades
#  22Apr2010  Kokron   Updated for Fortuna-2.1 on pleiades
#  21Jul2011  Kokron   Overlay older MKL module as on discover to gain reproducible results from dgeev in GSI
#  24Aug2012  Stassi   Added sh option to write bash source-able file
#  03Nov2016  Thompson Remove JIBB
########################################################################

# NOTE: Spell out scriptname--DO NOT SET EQUAL TO $0!
#----------------------------------------------------
set scriptname = g5_modules
if ( $scriptname == "$0:t" ) then
   set sourced = 0
else
   set sourced = 1
endif

# check for improper usage
#-------------------------
if (! $sourced && ! $#argv) goto usage   # not sourced and no args
if (  $sourced &&   $#argv) goto usage   # sourced with args

# get architecture, machine, and node information
#------------------------------------------------
set arch = `uname -s`
set mach = `uname -m`
set node = `uname -n`

# Find site
# ---------

if (($node =~ discover*) || ($node =~ borg*)  || \
    ($node =~ dali*)     || ($node =~ warp*)) then

   set site = "NCCS"

else if (($node =~ pfe*) || ($node =~ afe*) || \
         ($node =~ r[0-9]*i[0-9]*n[0-9]*) || \
         ($node =~ r[0-9]*c[0-9]*t[0-9]*n[0-9]*)) then

   # We are on NAS...
   set site = "NAS"

else if ( -d /ford1/share/gmao_SIteam/ && -d /ford1/local/ && $arch == Linux ) then
   set site = "GMAO.desktop"

else
   set site = $node

endif

alias echo2  "echo \!* > /dev/stderr"

# get values
#-----------
if ($mach == x86_64) goto X86_64
if ($mach == arm64)  goto ARM64

# ERROR -- unknown architecture/machine
#--------------------------------------
echo2 "${scriptname}: >>>WARNING<<< unrecognizable arch/machine = $arch/$mach"
echo 0   # indicating error condition
exit 8

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#                     X86_64 values (Linux)
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
X86_64:

#==================#
#  X86_64 default  #
#==================#

set modinit = DUMMY

set usemodules = 0

#========#
#  NCCS  #
#========#
if ( $site == NCCS ) then

   set usemod1 = /discover/swdev/gmao_SIteam/modulefiles-SLES15
   set usemod2 = /discover/swdev/jcsda/spack-stack/scu17/modulefiles
   set usemod3 = /gpfsm/dswdev/jcsda/spack-stack/scu17/spack-stack-1.9.0/envs/ue-oneapi-2024.2.0/install/modulefiles/Core

   set mod1 = stack-oneapi/2024.2.0
   set mod2 = stack-intel-oneapi-mpi/2021.13
   set mod3 = stack-python/3.11.7
   set mod4 = git
   set mod5 = cmake
   set mod6 = esmf/8.8.0
   set mod7 = gftl
   set mod8 = gftl-shared
   set mod9 = pflogger
   set mod10 = fargparse
   set mod11 = udunits

   set mods = ( $mod1 $mod2 $mod3 $mod4 $mod5 $mod6 $mod7 $mod8 $mod9 $mod10 $mod11 )
   set modinit = /usr/share/modules/init/csh

   set usemods = ( $usemod1 $usemod2 $usemod3 )
   set usemodules = 1

#=======#
#  NAS  #
#=======#
else if ( $site == NAS ) then

   set mod1 = GEOSenv

   set mod2 = comp-gcc/12.3.0-TOSS4
   set mod3 = comp-intel/2024.2.0-ifort
   set mod4 = mpi-hpe/mpt

   set mod5 = python/GEOSpyD/24.3.0-0/3.11

   set mods = ( $mod1 $mod2 $mod3 $mod4 $mod5 )
   set modinit = /usr/share/modules/init/tcsh

   set usemod1 = /u/scicon/tools/modulefiles
   set usemod2 = /nobackup/gmao_SIteam/modulefiles
   set usemod3 = /nasa/modulefiles/testing
   set usemods = ( $usemod1 $usemod2 $usemod3 )
   set usemodules = 1

#=================#
#  GMAO DESKTOP   #
#=================#
else if ( $site == GMAO.desktop ) then


   set mod1 = GEOSenv

   set mod2 = comp/gcc/12.1.0
   set mod3 = comp/intel/2024.2-ifort
   set mod4 = mpi/impi/2021.13
   set mod5 = other/python/GEOSpyD/24.7.1-0/3.11

   set mods = ( $mod1 $mod2 $mod3 $mod4 $mod5 )
   set modinit = /usr/share/Modules/init/tcsh

   set usemod1 = /ford1/share/gmao_SIteam/modulefiles
   set usemods = ( $usemod1 )
   set usemodules = 1

endif

goto ACTION

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#                     ARM64 values
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
ARM64:

#=================#
#  ARM64 default  #
#=================#

set modinit = DUMMY

set usemodules = 0

goto ACTION

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#              perform requested action
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
ACTION:

#===================#
#   ECHO VALUE(s)   #
#===================#
set wrapper = 0
if ( $#argv > 0 ) then

   if ( $1 == modules ) then
      echo $mods

   else if ( $1 == modinit ) then
      echo $modinit

   else if ( $1 == usemodules ) then
      echo $usemods

   else if ( $1 == ESMA_FC ) then
      echo $ESMA_FC

   else if ( $1 == site ) then
      echo $site

   else if ( $1 == sh ) then
      set wrapper = 1
      goto SETVALUES

   else if ( $1 == help ) then
      goto usage

   else
      echo2 "${scriptname}: >>>ERROR<<< unrecognizable option -- $1"
      echo2 Type "$scriptname help" for usage information.
      echo 0   # indicating error condition
      exit 9
   endif

   exit
endif

SETVALUES:
#================#
#   SET VALUES   #
#================#
#-------------------------------------------------------------------------
# set environment variables, and LD_LIBRARY_PATH; and load modules
#-------------------------------------------------------------------------

# add individual $ld_libraries to LD_LIBRARY_PATH, if not already there
#----------------------------------------------------------------------
   if ($?ld_libraries) then
      foreach lib ( $ld_libraries )
         if ($LD_LIBRARY_PATH !~ *$lib*) then
            setenv LD_LIBRARY_PATH ${LD_LIBRARY_PATH}:$lib
         endif
      end
   endif

endif

# load library modules
#---------------------
if (-e $modinit) then

   if (! $wrapper) echo -n " and modules"
   source $modinit
   module purge

   if ($usemodules) then
      foreach usemod ( $usemods )
         module use -a $usemod
      end
   endif

   foreach mod ( $mods )
      module load $mod
   end

   if ($?XTRAMODS2LOAD) then
      set xmods = (`echo $XTRAMODS2LOAD | cut -d: -f1- --output-delimiter=" "`)
      foreach mod ( $xmods )
         module load $mod
      end
   endif

endif
if (! $wrapper) echo " for $node"

# write sh commands to a .g5_modules.sh file
#-------------------------------------------
if ($wrapper) then
   set outfil = ".g5_modules.sh"

   if ($?LD_LIBRARY_PATH) then
      echo "export LD_LIBRARY_PATH=$LD_LIBRARY_PATH"     >> $outfil
   endif
   if ($?LD_LIBRARY64_PATH) then
      echo "export LD_LIBRARY64_PATH=$LD_LIBRARY64_PATH" >> $outfil
   endif
   if ($?UDUNITS2_XML_PATH) then
      echo "export UDUNITS2_XML_PATH=$UDUNITS2_XML_PATH" >> $outfil
   endif

   set modulecmd = `which modulecmd`
   if ($status) set modulecmd = "/usr/bin/modulecmd"
   set modinit_sh = `echo $modinit | sed "s|/csh|/sh|"`

   if (-e $modulecmd) then
      if (-e $modinit_sh) then
         echo 'source '$modinit_sh                       >> $outfil
      endif
      echo 'eval `'$modulecmd sh purge'`'                >> $outfil
      foreach mod ($mods)
         echo 'eval `'$modulecmd sh load $mod'`'         >> $outfil
      end
   endif

endif
exit

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#                    usage information                    #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
usage:

more <<EOF

NAME
     $scriptname - Script to handle library module definitions

DESCRIPTION

     This script provides a single location for storing the
     library module name information.  Previous to this script, this
     information was coded in multiple files.

     If the script is called with "modules", "modinit", or
     then it will echo the values to standard output without
     modifying the environment.

SYNOPSIS

     source $scriptname

     or

     $scriptname <option>

OPTIONS

     help                echo usage
     modules             echo expected list of modules
     modinit             echo location of csh module initialization script
     usemodules          echo logical indicating whether "module use directory(s)"
                              is needed prior to loading other modules
     ESMA_FC             echo value of ESMA_FC if set
     site                echo site as detected by $scriptname
     sh                  write sh version of g5_modules to file named, ".g5_modules.sh"

EOF
exit 1
